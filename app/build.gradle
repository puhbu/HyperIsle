plugins {
    id 'org.jetbrains.kotlin.android'
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.materialthemebuilder)
}

def keyProps = new Properties()
def keyPropsFile = rootProject.file('keystore/keystore.properties')
if (keyPropsFile.exists()) {
    keyProps.load(new FileInputStream(keyPropsFile))
}

def versionProps = new Properties()
def versionPropsFile = rootProject.file('version.properties')
if (versionPropsFile.exists()) {
    versionProps.load(new FileInputStream(versionPropsFile))
}

android {
    namespace 'art.luaj.hyperisle'
    compileSdk 34

    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    defaultConfig {
        applicationId "art.luaj.hyperisle"
        minSdk 33
        targetSdk 34
        versionCode versionProps['versionCode'].toInteger()
        versionName versionProps['versionName']

        buildConfigField "String", "BUILD_TIME", "\"${new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("UTC"))}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        resources {
            excludes += "META-INF/**"
            excludes += "kotlin/**"
            excludes += "kotlin-tooling-metadata.json"
            excludes += "okhttp3/**"
            excludes += "org/**"
            excludes += "**.properties"
            excludes += "**.bin"
        }
    }

    signingConfigs {
        release {
            keyAlias keyProps['keyAlias']
            keyPassword keyProps['keyPassword']
            storeFile keyProps['storeFile'] ? file(keyProps['storeFile']) : null
            storePassword keyProps['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

materialThemeBuilder {
    themes {
        [
                ["Red", "F44336"],
                ["Pink", "E91E63"],
                ["Purple", "9C27B0"],
                ["DeepPurple", "673AB7"],
                ["Indigo", "3F51B5"],
                ["Blue", "2196F3"],
                ["LightBlue", "03A9F4"],
                ["Cyan", "00BCD4"],
                ["Teal", "009688"],
                ["Green", "4FAF50"],
                ["LightGreen", "8BC3A4"],
                ["Lime", "CDDC39"],
                ["Yellow", "FFEB3B"],
                ["Amber", "FFC107"],
                ["Orange", "FF9800"],
                ["DeepOrange", "FF5722"],
                ["Brown", "795548"],
                ["BlueGrey", "607D8F"],
                ["Sakura", "FF9CA8"]
        ].each { color ->
            def name = color[0]
            def value = color[1]
            create("Material${name}") {
                lightThemeFormat = "ThemeOverlay.Light.%s"
                darkThemeFormat = "ThemeOverlay.Dark.%s"
                primaryColor = "#${value}"
            }
        }
    }
    generatePalette = true
}

dependencies {
    compileOnly fileTree(dir: 'libs/compile_only', include: ['*.jar'])
    implementation(libs.androidx.activity)
    implementation(libs.androidx.browser)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.core)
    implementation(libs.androidx.fragment)
    implementation(libs.androidx.navigation.fragment)
    implementation(libs.androidx.navigation.ui)
    implementation(libs.androidx.preference)
    implementation(libs.androidx.recyclerview)
    implementation(libs.androidx.swiperefreshlayout)
    implementation(libs.material)
    implementation(libs.bundles.rikkax)
}

configurations.configureEach {
    exclude group: "androidx.appcompat", module: "appcompat"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
}